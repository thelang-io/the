/*!
 * Copyright (c) Aaron Delasy
 * Licensed under the MIT License
 */

import Token_toText from "./text"
import * as Reader from "./reader"
import * as Token from "./token"
import * as errors from "./errors"
import * as t from "./types"

export obj Tokenizer {
  mut data: t.Token[]
  mut dataLen: int
  mut errors: str[]
  mut reader: ref Reader.Reader
  mut state: t.TokenizerState

  fn errorSeek (mut self: ref Self, pos: int) {
    if pos == -1 {
      self.errors.clear()
    } else {
      self.errors = self.errors.slice(0, pos + 1)
    }
  }

  fn lookahead (mut self: ref Self, t: t.TokenType, alwaysGoBack := false, allowComments := false) bool {
    startPos := self.state.idx
    tk := self.next(allowComments: allowComments)
    r := tk.t == t

    if alwaysGoBack || !r {
      self.state.idx = startPos
    }

    return r
  }

  fn next (mut self: ref Self, allowComments := false) t.Token {
    if self.dataLen != 0 && (self.state.idx == self.dataLen && self.data[-1].t == .Eof) {
      throw error_NewError("Tried next on Tokenizer eof")
    }

    loop {
      if self.dataLen <= self.state.idx {
        self.data.push(self._getToken())
        self.dataLen++
      }

      tk := self.data[self.state.idx++]

      if (allowComments || (tk.t != .CommentBlock && tk.t != .CommentLine)) && tk.t != .Whitespace {
        return tk
      }
    }
  }

  fn pos (mut self: ref Self) int {
    return self.state.idx
  }

  fn readerStartPos (mut self: ref Self, offset := 0) int {
    return self.state.idx == 0 ? 0 : self.data[self.state.idx - 1 + offset].start
  }

  fn readerEndPos (mut self: ref Self) int {
    return self.state.idx == 0 ? 0 : self.data[self.state.idx - 1].end
  }

  fn readerSeek (mut self: ref Self, pos: int) int {
    loop i := self.dataLen - 1; i >= 0; i-- {
      if self.data[i].start <= pos && pos <= self.data[i].end {
        self.seek(i)
        return
      }
    }

    self.seek(0)
  }

  fn seek (mut self: ref Self, pos: int) {
    self.state.idx = pos
  }

  fn _getToken (mut self: ref Self) t.Token {
    if self.reader.eof() {
      return t.Token{
        t: .Eof,
        val: "",
        start: self.state.pos,
        end: self.state.pos
      }
    }

    self.state.ch = self.reader.next()
    self.state.handled = false
    mut tk: t.Token?

    if (
      (tk = self._maybeWhitespace()) != nil ||
      (tk = self._maybeCommentBlock()) != nil ||
      (tk = self._maybeCommentLine()) != nil ||
      (tk = self._maybeOp()) != nil ||
      (tk = self._maybeKeyword()) != nil ||
      (tk = self._maybeNumber()) != nil ||
      (tk = self._maybeString()) != nil ||
      (tk = self._maybeChar()) != nil
    ) {
      return tk
    } else {
      self._raise(errors.E0000(self.state.ch), self.state.pos)
      return self._wrapToken(.Unknown)
    }
  }

  fn _maybeChar (mut self: ref Self) t.Token? {
    if self.state.ch != '\'' {
      return nil
    }

    if self.reader.eof() {
      self._raise(errors.E0002(), self.state.pos)
      return self._wrapToken(.LitChar)
    }

    pos1 := self.reader.pos
    ch1 := self.reader.next()

    if ch1 == '\n' {
      self._raise(errors.E0002(), self.state.pos)
      return self._wrapToken(.LitChar)
    } elif ch1 == '\'' {
      self._raise(errors.E0004(), self.state.pos)
      return self._wrapToken(.LitChar)
    } elif ch1 == '\\' {
      if self.reader.eof() {
        self._raise(errors.E0002(), self.state.pos)
        return self._wrapToken(.LitChar)
      }

      ch2 := self.reader.next()

      if !Token.isCharEsc(ch2) {
        self._raise(errors.E0005(), pos1)
      }
    }

    if !self.reader.lookahead('\'') {
      loop {
        if self.reader.eof() {
          self._raise(errors.E0002(), self.state.pos)
          break
        }

        pos3 := self.reader.pos
        ch3 := self.reader.next()

        if ch3 == '\'' {
          self._raise(errors.E0007(), self.state.pos)
          break
        } elif ch3 == '\n' {
          self.reader.seek(pos3)
          self._raise(errors.E0002(), self.state.pos)
          break
        }
      }
    }

    return self._wrapToken(.LitChar)
  }

  fn _maybeCommentBlock (mut self: ref Self) t.Token? {
    if self.state.ch != '/' || !self.reader.lookahead('*') {
      return nil
    }

    loop {
      if self.reader.eof() {
        self._raise(errors.E0001(), self.state.pos)
        break
      }

      ch := self.reader.next()

      if ch == '*' && self.reader.lookahead('/') {
        break
      }
    }

    return self._wrapToken(.CommentBlock)
  }

  fn _maybeCommentLine (mut self: ref Self) t.Token? {
    if self.state.ch != '/' || !self.reader.lookahead('/') {
      return nil
    }

    self.reader.walk(Token.isNotNewLine)
    return self._wrapToken(.CommentLine)
  }

  fn _maybeKeyword (mut self: ref Self) t.Token? {
    if !Token.isIdStart(self.state.ch) {
      return nil
    }

    self.reader.walk(Token.isId)
    val := self.reader.slice(self.state.pos, self.reader.pos)

    if val == "as" { return self._wrapToken(.KwAs) }
    if val == "async" { return self._wrapToken(.KwAsync) }
    if val == "await" { return self._wrapToken(.KwAwait) }
    if val == "break" { return self._wrapToken(.KwBreak) }
    if val == "catch" { return self._wrapToken(.KwCatch) }
    if val == "const" { return self._wrapToken(.KwConst) }
    if val == "continue" { return self._wrapToken(.KwContinue) }
    if val == "elif" { return self._wrapToken(.KwElif) }
    if val == "else" { return self._wrapToken(.KwElse) }
    if val == "enum" { return self._wrapToken(.KwEnum) }
    if val == "export" { return self._wrapToken(.KwExport) }
    if val == "false" { return self._wrapToken(.KwFalse) }
    if val == "fn" { return self._wrapToken(.KwFn) }
    if val == "from" { return self._wrapToken(.KwFrom) }
    if val == "if" { return self._wrapToken(.KwIf) }
    if val == "import" { return self._wrapToken(.KwImport) }
    if val == "is" { return self._wrapToken(.KwIs) }
    if val == "loop" { return self._wrapToken(.KwLoop) }
    if val == "main" { return self._wrapToken(.KwMain) }
    if val == "mut" { return self._wrapToken(.KwMut) }
    if val == "nil" { return self._wrapToken(.KwNil) }
    if val == "obj" { return self._wrapToken(.KwObj) }
    if val == "ref" { return self._wrapToken(.KwRef) }
    if val == "return" { return self._wrapToken(.KwReturn) }
    if val == "throw" { return self._wrapToken(.KwThrow) }
    if val == "true" { return self._wrapToken(.KwTrue) }
    if val == "try" { return self._wrapToken(.KwTry) }
    if val == "type" { return self._wrapToken(.KwType) }

    return self._wrapToken(.Id)
  }

  fn _maybeNumber (mut self: ref Self) t.Token? {
    if !self.state.ch.isDigit {
      return nil
    }

    if self.state.ch == '0' {
      if self.reader.eof() {
        return self._wrapToken(.LitIntDec)
      }

      pos := self.reader.pos
      ch := self.reader.next()

      if Token.isIntDec(ch) {
        self.reader.seek(pos)
        self.reader.walk(Token.isId)
        self._raise(errors.E0008(), self.state.pos)

        return self._wrapToken(.LitIntDec)
      } elif ch == 'B' || ch == 'b' {
        return self._wrapInt(.LitIntBin, errors.E0009("binary"), Token.isIntBin)
      } elif ch == 'X' || ch == 'x' {
        return self._wrapInt(.LitIntHex, errors.E0009("hexadecimal"), Token.isIntHex)
      } elif ch == 'O' || ch == 'o' {
        return self._wrapInt(.LitIntOct, errors.E0009("octal"), Token.isIntOct)
      }

      self.reader.seek(pos)
    } else {
      self.reader.walk(Token.isIntDec)
    }

    return self._wrapInt(.LitIntDec, errors.E0009("decimal"), Token.isIntDec)
  }

  fn _maybeOp (mut self: ref Self) t.Token? {
    if self.state.ch == '&' {
      if self.reader.lookahead('&') {
        if self.reader.lookahead('=') {
          return self._wrapToken(.OpAmpAmpEq)
        } else {
          return self._wrapToken(.OpAmpAmp)
        }
      } elif self.reader.lookahead('=') {
        return self._wrapToken(.OpAmpEq)
      } else {
        return self._wrapToken(.OpAmp)
      }
    } elif self.state.ch == '@' {
      return self._wrapToken(.OpAt)
    } elif self.state.ch == '`' {
      return self._wrapToken(.OpBacktick)
    } elif self.state.ch == '\\' {
      return self._wrapToken(.OpBackslash)
    } elif self.state.ch == '^' {
      if self.reader.lookahead('=') {
        return self._wrapToken(.OpCaretEq)
      } else {
        return self._wrapToken(.OpCaret)
      }
    } elif self.state.ch == ':' {
      if self.reader.lookahead('=') {
        return self._wrapToken(.OpColonEq)
      } else {
        return self._wrapToken(.OpColon)
      }
    } elif self.state.ch == ',' {
      return self._wrapToken(.OpComma)
    } elif self.state.ch == '.' {
      pos := self.reader.pos

      if self.reader.lookahead('.') && self.reader.lookahead('.') {
        return self._wrapToken(.OpEllipsis)
      }

      self.reader.seek(pos)
      return self._wrapToken(.OpDot)
    } elif self.state.ch == '$' {
      return self._wrapToken(.OpDollar)
    } elif self.state.ch == '=' {
      if self.reader.lookahead('=') {
        return self._wrapToken(.OpEqEq)
      } else {
        return self._wrapToken(.OpEq)
      }
    } elif self.state.ch == '!' {
      if self.reader.lookahead('=') {
        return self._wrapToken(.OpExclEq)
      } else {
        return self._wrapToken(.OpExcl)
      }
    } elif self.state.ch == '>' {
      if self.reader.lookahead('>') {
        if self.reader.lookahead('=') {
          return self._wrapToken(.OpRShiftEq)
        } else {
          return self._wrapToken(.OpRShift)
        }
      } elif self.reader.lookahead('=') {
        return self._wrapToken(.OpGtEq)
      } else {
        return self._wrapToken(.OpGt)
      }
    } elif self.state.ch == '#' {
      return self._wrapToken(.OpHash)
    } elif self.state.ch == '{' {
      return self._wrapToken(.OpLBrace)
    } elif self.state.ch == '[' {
      return self._wrapToken(.OpLBrack)
    } elif self.state.ch == '(' {
      return self._wrapToken(.OpLPar)
    } elif self.state.ch == '<' {
      if self.reader.lookahead('<') {
        if self.reader.lookahead('=') {
          return self._wrapToken(.OpLShiftEq)
        } else {
          return self._wrapToken(.OpLShift)
        }
      } elif self.reader.lookahead('=') {
        return self._wrapToken(.OpLtEq)
      } else {
        return self._wrapToken(.OpLt)
      }
    } elif self.state.ch == '-' {
      if self.reader.lookahead('-') {
        return self._wrapToken(.OpMinusMinus)
      } elif self.reader.lookahead('=') {
        return self._wrapToken(.OpMinusEq)
      } elif self.reader.lookahead('>') {
        return self._wrapToken(.OpArrow)
      } else {
        return self._wrapToken(.OpMinus)
      }
    } elif self.state.ch == '|' {
      if self.reader.lookahead('|') {
        if self.reader.lookahead('=') {
          return self._wrapToken(.OpPipePipeEq)
        } else {
          return self._wrapToken(.OpPipePipe)
        }
      } elif self.reader.lookahead('=') {
        return self._wrapToken(.OpPipeEq)
      } else {
        return self._wrapToken(.OpPipe)
      }
    } elif self.state.ch == '%' {
      if self.reader.lookahead('=') {
        return self._wrapToken(.OpPercentEq)
      } else {
        return self._wrapToken(.OpPercent)
      }
    } elif self.state.ch == '+' {
      if self.reader.lookahead('+') {
        return self._wrapToken(.OpPlusPlus)
      } elif self.reader.lookahead('=') {
        return self._wrapToken(.OpPlusEq)
      } else {
        return self._wrapToken(.OpPlus)
      }
    } elif self.state.ch == '?' {
      return self._wrapToken(.OpQn)
    } elif self.state.ch == '}' {
      return self._wrapToken(.OpRBrace)
    } elif self.state.ch == ']' {
      return self._wrapToken(.OpRBrack)
    } elif self.state.ch == ')' {
      return self._wrapToken(.OpRPar)
    } elif self.state.ch == ';' {
      return self._wrapToken(.OpSemi)
    } elif self.state.ch == '/' {
      if self.reader.lookahead('=') {
        return self._wrapToken(.OpSlashEq)
      } else {
        return self._wrapToken(.OpSlash)
      }
    } elif self.state.ch == '*' {
      if self.reader.lookahead('=') {
        return self._wrapToken(.OpStarEq)
      } else {
        return self._wrapToken(.OpStar)
      }
    } elif self.state.ch == '~' {
      return self._wrapToken(.OpTilde)
    }

    return nil
  }

  fn _maybeString (mut self: ref Self) t.Token? {
    if self.state.ch != '"' {
      return nil
    }

    loop {
      if self.reader.eof() {
        self._raise(errors.E0003(), self.state.pos)
        break
      }

      pos1 := self.reader.pos
      ch1 := self.reader.next()

      if ch1 == '"' {
        break
      } elif ch1 == '\\' {
        ch2 := self.reader.next()

        if !Token.isStrEsc(ch2) {
          self._raise(errors.E0006(), pos1)
        }
      }
    }

    return self._wrapToken(.LitStr)
  }

  fn _maybeWhitespace (mut self: ref Self) t.Token? {
    if !self.state.ch.isWhitespace {
      return nil
    }

    self.reader.walk(Token.isWhitespace)
    return self._wrapToken(.Whitespace)
  }

  fn _raise (mut self: ref Self, message: str, start: int, end := self.reader.pos) {
    if self.state.handled {
      return
    }

    self._raiseException("SyntaxError", message, start, end)
    self.state.handled = true
  }

  fn _raiseException (mut self: ref Self, errorType: str, message: str, start: int, end: int) {
    error := Reader.error(self.reader, errorType, message, start, end)
    self.errors.push(error)
  }

  fn _wrapFloat (mut self: ref Self, t: t.TokenType, errorMessage: str) t.Token {
    if self.reader.eof() {
      return self._wrapToken(t)
    }

    pos1 := self.reader.pos
    ch1 := self.reader.next()

    if Token.isId(ch1) && ch1 != 'E' && ch1 != 'e' {
      self.reader.seek(pos1)
      self.reader.walk(Token.isId)
      self._raise(errorMessage, self.state.pos)

      return self._wrapToken(t)
    } elif ch1 != '.' && ch1 != 'E' && ch1 != 'e' {
      self.reader.seek(pos1)
      return self._wrapToken(t)
    }

    mut expStartPos := pos1

    if ch1 == '.' {
      if self.reader.eof() {
        self._raise(errors.E0010(), self.state.pos)
        return self._wrapToken(.LitFloat)
      }

      pos2 := self.reader.pos
      ch2 := self.reader.next()

      if ch2 == '.' {
        self.reader.seek(pos1)
        return self._wrapToken(t)
      } elif !Token.isIntDec(ch2) {
        self.reader.seek(pos2)
        self.reader.walk(Token.isId)
        self._raise(errors.E0010(), self.state.pos)

        return self._wrapToken(.LitFloat)
      }

      self.reader.walk(Token.isIntDec)

      if self.reader.eof() {
        return self._wrapTokenFloat(t)
      }

      pos3 := self.reader.pos
      ch3 := self.reader.next()

      if Token.isId(ch3) && ch3 != 'E' && ch3 != 'e' {
        self.reader.seek(pos3)
        self.reader.walk(Token.isId)
        self._raise(errors.E0010(), self.state.pos)

        return self._wrapToken(.LitFloat)
      } elif ch3 != 'E' && ch3 != 'e' {
        self.reader.seek(pos3)
        return self._wrapTokenFloat(t)
      }

      expStartPos = pos3
    }

    if self.reader.eof() {
      self._raise(errors.E0011(), expStartPos)
      return self._wrapToken(.LitFloat)
    }

    pos4 := self.reader.pos
    ch4 := self.reader.next()

    if !Token.isIntDec(ch4) && ch4 != '+' && ch4 != '-' {
      self.reader.seek(pos4)
      self.reader.walk(Token.isId)
      self._raise(errors.E0011(), expStartPos)

      return self._wrapToken(.LitFloat)
    }

    if ch4 == '+' || ch4 == '-' {
      if self.reader.eof() {
        self._raise(errors.E0011(), expStartPos)
        return self._wrapToken(.LitFloat)
      }

      pos5 := self.reader.pos
      ch5 := self.reader.next()

      if !Token.isIntDec(ch5) {
        self.reader.seek(pos5)
        self.reader.walk(Token.isId)
        self._raise(errors.E0011(), expStartPos)

        return self._wrapToken(.LitFloat)
      }
    }

    self.reader.walk(Token.isIntDec)
    return self._wrapTokenFloat(t)
  }

  fn _wrapInt (mut self: ref Self, t: t.TokenType, errorMessage: str, check: (char) -> bool) t.Token {
    if t != .LitIntDec {
      if self.reader.eof() {
        self._raise(errorMessage, self.state.pos)
        return self._wrapToken(t)
      }

      pos := self.reader.pos
      ch := self.reader.next()

      if !check(ch) {
        self.reader.seek(pos)
        self.reader.walk(Token.isId)
        self._raise(errorMessage, self.state.pos)

        return self._wrapToken(t)
      }

      self.reader.walk(check)
    }

    return self._wrapFloat(t, errorMessage)
  }

  fn _wrapToken (mut self: ref Self, t: t.TokenType) t.Token {
    start := self.state.pos
    self.state.pos = self.reader.pos
    self.state.handled = true

    return t.Token{
      t: t,
      val: self.reader.slice(start, self.state.pos),
      start: start,
      end: self.state.pos
    }
  }

  fn _wrapTokenFloat (mut self: ref Self, t: t.TokenType) t.Token {
    if !self.reader.eof() {
      pos := self.reader.pos
      ch := self.reader.next()

      if Token.isId(ch) {
        self.reader.seek(pos)
        self.reader.walk(Token.isId)
        self._raise(errors.E0010(), self.state.pos)

        return self._wrapToken(.LitFloat)
      }

      self.reader.seek(pos)
    }

    if t == .LitIntBin {
      self._raise(errors.E0012("binary"), self.state.pos)
    } elif t == .LitIntHex {
      self._raise(errors.E0012("hexadecimal"), self.state.pos)
    } elif t == .LitIntOct {
      self._raise(errors.E0012("octal"), self.state.pos)
    }

    return self._wrapToken(.LitFloat)
  }
}

export fn init (mut reader: ref Reader.Reader) Tokenizer {
  return Tokenizer{reader: reader}
}

export fn stringify (mut tokenizer: ref Tokenizer, allowComments: bool) str {
  mut result := ""

  loop {
    tk := tokenizer.next(allowComments: allowComments)
    result += Token_toText(tk) + os_EOL
    if tk.t == .Eof { break }
  }

  return tokenizer.errors.empty ? result : tokenizer.errors.join(os_EOL)
}
