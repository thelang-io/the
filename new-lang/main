/*!
 * Copyright (c) Aaron Delasy
 *
 * Unauthorized copying of this file, via any medium is strictly prohibited
 * Proprietary and confidential
 */

import EOL from "os"
import Error, NewError from "error"

import Reader, Reader_init from "./reader"
import TokenType, Token_str from "./token"
import Tokenizer_init, Tokenizer_next from "./tokenizer"

main {
  try {
    if argv.len == 1 {
      throw NewError("REPL is not supported")
    }

    mut fileName: str?

    loop mut i := 1; i < argv.len; i++ {
      arg := argv[i]

      if arg[0] == '-' {
        throw NewError("Bad option " + arg)
      } elif fileName == nil {
        fileName = arg
      } else {
        throw NewError("Processing multiple files is not supported")
      }
    }

    if fileName == nil {
      throw NewError("File is not specified")
    }

    mut reader := Reader_init(fileName)
    mut tokenizer := Tokenizer_init(reader)
    mut result: str[]

    loop (
      mut tok := Tokenizer_next(tokenizer);
      tok.type != TK_EOF;
      tok = Tokenizer_next(tokenizer)
    ) {
      result.push(Token_str(tok))
    }

    if tokenizer.errors.len != 0 {
      print(tokenizer.errors.join(EOL), to: stderr)
    } else {
      print(result.join(EOL))
    }
  } catch err: Error {
    print(err.name + ": " + err.message, to: stderr)
    exit(EXIT_FAILURE)
  }
}
